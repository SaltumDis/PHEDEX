#!/usr/bin/env perl

##H Delete replica entries from fake nodes.
##H
##H This agent clears out replica entries for nodes that are not
##H real disk buffers.  It deletes all replicas created more than
##H three days ago that have no outbound transfers.  In nature
##H this is like FileDiskCleaner, except it has no disk to worry
##H about.
##H
##H Usage:
##H   FileFakeCleaner
##H      -state DIRECTORY -nodes NAME[,NAME...]
##H      -db FILE[:SECTION] [-wait SECS]
##H
##H -state     agent state directory
##H -nodes     comma-separated list of nodes to run the agent for
##H -db        database connection configuration parameter file
##H -wait      time to wait in seconds between work scans

BEGIN {
  use strict; use warnings;
  our $me = $0; $me =~ s|.*/||;
  our $home = $0; $home =~ s|/[^/]+$||; $home ||= "."; $home .= "/../../Toolkit/Common";
  unshift(@INC, $home);
}

######################################################################
use UtilsHelp;
my %args = (WAITTIME => 8*3600);
while (scalar @ARGV)
{
    if ($ARGV[0] eq '-wait' && scalar @ARGV > 1)
    { shift (@ARGV); $args{WAITTIME} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-state' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DROPDIR} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-db' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DBCONFIG} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-nodes' && scalar @ARGV > 1)
    { shift (@ARGV); push(@{$args{NODES}}, split(/,/, shift(@ARGV))); }
    elsif ($ARGV[0] eq '-h')
    { &usage(); }
    else
    { last; }
}

if (@ARGV || !$args{DROPDIR} || !$args{NODES} || !$args{DBCONFIG})
{
    die "Insufficient parameters, use -h for help.\n";
}

my $agent = new FileFakeCleaner (%args);
# Recapture interrupt signal, oracle swallows it.
$SIG{INT} = sub { system "touch $agent->{STOPFLAG}"; $agent->maybeStop (); };
$agent->process ();

######################################################################
# Routines for this agent.
package FileFakeCleaner; use strict; use warnings; use base 'UtilsAgent';
use UtilsCommand;
use UtilsLogging;
use UtilsTiming;
use UtilsCatalogue;
use UtilsDB;

sub new
{
    my $proto = shift;
    my $class = ref($proto) || $proto;
    my $self = $class->SUPER::new(@_);
    my %params = (DBCONFIG => undef,		# Database configuration file
	  	  NODES => undef,		# Nodes to clean tables for
	  	  MYNODE => undef,		# My TMDB node name
	  	  MSSNODE => undef,		# MSS TMDB node name
	  	  AGENTID => "Cleaner");	# Identity for activity logs
    my %args = (@_);
    map { $self->{$_} = $args{$_} || $params{$_} } keys %params;
    bless $self, $class;
    return $self;
}

# Called by agent main routine before sleeping.  Pick up work.
sub idle
{
    my ($self, @pending) = @_;
    my $dbh = undef;
    eval
    {
	foreach my $node (@{$self->{NODES}})
	{
	    my $start = &mytimeofday();
	    $self->{MYNODE} = $node;
	    $dbh = &connectToDatabase ($self) or die "failed to connect";

	    my ($stmt, $ndel) = &dbexec($dbh, qq{
		delete from t_replica_state
		where timestamp < :old
		  and node = :node
		  and guid not in (select guid from t_transfer_state
	  			   where to_node = :node or from_node = :node)},
		":old" => $start - 3*86400,
		":node" => $self->{MYNODE});
	    $dbh->commit();
	    &logmsg ("deleted $ndel replicas for $node in "
		     . sprintf ('%.1fs', &mytimeofday()-$start))
	        if $ndel > 0;
	    undef $self->{MYNODE};
	}
    };
    do { &alert ("database error: $@"); $dbh->rollback() if $dbh; } if $@;

    # Disconnect from the database
    &disconnectFromDatabase ($self, $dbh);

    # Check children are still running and then wait
    $self->nap ($self->{WAITTIME});
}
