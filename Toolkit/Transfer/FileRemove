#! /usr/bin/env perl

##H Clear up files marked for deletion.
##H
##H This agent perfoms the deletion requests entered into TMDB
##H by an administrator or via the web page.  Each request consists
##H of a dataset or block plus the node from where the file should
##H be deleted.
##H
##H Files on nodes without pending transfers are candidates for
##H clearing up.  Files are removed oldest first, first from the
##H TMDB replica table, then physically from disk.  File removal
##H may be a real removal, or simply ejection of the file from
##H stager disk but not permanently removed from tape.
##H
##H By default this agent is limited to pulling 100 files per minute
##H from the TMDB and creating deletion jobs.  This rate may be
##H increased using the -limit option, but one should be mindful of
##H the load the deletion command has on the storage systems
##H
##H Usage:
##H   FileRemove
##H      -state DIRECTORY -nodes NAME -db FILE[:SECTION] [-log OUT]
##H      -storagemap FILE [-protocol NAME] -delete COMMAND[,ARG...]
##H      [-limit N]
##H
##H -state         agent state directory.
##H -nodes         nodes to run and clean files for.
##H -db            database connection configuration parameter file.
##H -storagemap    storage mapping catalogue.
##H -protocol      protocol to use with storage map, by default "direct".
##H -delete        command to remove physical copy (typically FileDownloadDelete).
##H -limit         limit on the number of files to delete per minute, default 100
##H -log           where to redirect logging information.

######################################################################
use Getopt::Long;
use PHEDEX::Core::Help;

&GetOptions ("state=s"     => \$args{DROPDIR},
	     "log=s"       => \$args{LOGFILE},
	     "db=s"        => \$args{DBCONFIG},
             "nodes=s"     => sub { push(@{$args{NODES}}, split(/,/, $_[1])) },
             "delete=s"    => sub { push(@{$args{CMD_RM}}, split(/,/, $_[1])) },
	     "storagemap=s"=> \$args{STORAGEMAP},
	     "protocol=s"  => \$args{PROTOCOL},
	     "limit=i"     => \$args{LIMIT},
	     "help|h"	   => sub { &usage() });

if (@ARGV || !$args{NODES} || !$args{DROPDIR} || !$args{DBCONFIG}
    || !$args{CMD_RM} || !$args{STORAGEMAP})
{
    die "Insufficient parameters, use -h for help.\n";
}

(new FileRemove (%args))->process();

################################################
package FileRemove; use strict; use warnings; use base 'PHEDEX::Core::Agent';
use File::Path;
use Data::Dumper;

use PHEDEX::Core::Command;
use PHEDEX::Core::Logging;
use PHEDEX::Core::Timing;
use PHEDEX::Core::Catalogue;
use PHEDEX::Core::DB;
use PHEDEX::Core::RFIO;

sub new
{
    my $proto = shift;
    my $class = ref($proto) || $proto;
    my $self = $class->SUPER::new(@_);
    my %params = (DBCONFIG => undef,		# Database configuration file
	  	  NODES    => undef,		# Node names to run this agent for
		  WAITTIME => 60 + rand(15),	# Agent activity cycle
		  DELETING => undef,		# Are we deleting files now?
		  CMD_RM   => undef,            # cmd to remove physical files
		  PROTOCOL => 'direct',         # File access protocol
	  	  STORAGEMAP => undef,		# Storage path mapping rules
		  LIMIT => 100                  # Max number of files per cycle
		  );
    
    my %args = (@_);
    map { $$self{$_} = $args{$_} || $params{$_} } keys %params;
    bless $self, $class;
    return $self;
}

# Delete a file.  We do one step at a time; if the step fails, we just
# tell the caller to come back here again at a more opportune time.
# The steps are ordered such that they are safe to execute several
# times if we have to give for one reason or another.
sub deleteOneFile
{
    my ($self, $drop, $file) = @_;
    my $dbh = undef;
    my @nodes = ();

    my $status = eval {
	$dbh = $self->connectAgent();
	@nodes = $self->expandNodes();

	# Get Buffer Node if this is for an MSS node.  We will check
	# transfers for the buffer node along with the MSS node, and
	# delete replicas for the buffer node as well as the MSS node.
	# We will only mark the deletion done for the MSS node
	# however.
	# TODO:  Make this feature naming-convention independent
	my %node_binds = (':node1' => $$file{NODEID});
	if ($$file{NODE} =~ /_MSS$/) {
	    my $buffer = $$file{NODE};
	    $buffer =~ s/_MSS$/_Buffer/;
	    my ($buffer_id) = &dbexec($dbh, qq{ select id from t_adm_node where name = :buffer },
				      ':buffer' => $buffer)->fetchrow();
	    $node_binds{':node2'} = $buffer_id if $buffer_id;
	}
	my $node_list = join(',', keys %node_binds);

	
	# Make sure the file is still safe to delete.  More transfers
	# out might have been created for this file while we were not
	# minding this particular file (sleeping or deleting things).
	my ($npending) = &dbexec($dbh, qq{
	    select count(fileid) from t_xfer_task xt
	       left join t_xfer_task_done xtd
	       on xt.id=xtd.task
	    where xt.from_node in ($node_list)
	    and xt.fileid = :fileid
	    and xtd.task is null},
	    %node_binds,
	    ":fileid" => $$file{FILEID})
	    ->fetchrow();
	if ($npending)
	{
	    &warn ("not removing $$file{LFN}, $npending pending transfers");
	    return 0;
	}
	
	# Now delete the replica entry to avoid new transfer edges.
	&dbexec($dbh, qq{
	    delete from t_xfer_replica where fileid = :fileid and node in ($node_list)},
	    ":fileid" => $$file{FILEID}, %node_binds);
	    
	# Issue file removal from disk now.
	my $log = "$$self{DROPDIR}/@{[time()]}.$$file{NODEID}.$$file{FILEID}.log";
	$self->addJob( sub { $self->deleteJob ($file, @_) },
		      { TIMEOUT => 30, LOGFILE => $log, DB => $dbh },
		      (@{$$self{CMD_RM}}, 'post', $$file{PFN}) );


	# Report completition time to DB. If the physical deletion fails,
	# we will roll back.
	my $now = &mytimeofday();
	&dbexec($dbh, qq{
	    update t_xfer_delete set time_complete = :now
		where fileid = :fileid and node = :node},
		":fileid" => $$file{FILEID}, ":node" => $$file{NODEID}, ":now" => $now);
	return 1;
    };

    do { chomp ($@); &alert ("database error: $@");
	 eval { $dbh->rollback() } if $dbh;
	 $status = 0 } if $@;
    
    # Return status code to caller
    return $status;
}


sub deleteJob
{
    my ($self, $file, $job) = @_;
    if ($$job{STATUS})
    {
	&warn("failed to delete file $$file{PFN} at node "
	      ."$$file{NODE}, log in $$job{LOGFILE}");
    }
    else
    {
	&logmsg("deleted file $$file{PFN} at node $$file{NODE}");
	unlink ($$job{LOGFILE});
    }
}


sub processDrop
{
    my ($self, $drop) = @_;

    # Sanity checking
    return if (! $self->inspectDrop ($drop));
    delete $$self{BAD}{$drop};
    &timeStart($$self{STARTTIME});

    # Read back file information
    my $dropdir = "$$self{WORKDIR}/$drop";
    my $file = do { no strict "vars"; eval &input ("$dropdir/packet") };
    if ($@ || !$file || !$$file{FILEID} || !$$file{LFN} || !$$file{PFN} || !$$file{TIME_START})
    {
	&alert ("corrupt packet in $drop");
	$self->markBad ($drop);
	return;
    }

    # Try deleting this file.  If something fails, keep this drop as
    # is, we'll come back to it later.
    return unless $self->deleteOneFile ($drop, $file);

    # Mark drop done so it will be nuked
    &touch ("$dropdir/done");

    # Log transfer delay stats
    my $dtransfer = &mytimeofday() - $$file{TIME_START};
    &logmsg ("xstats: $$file{NODE} " . sprintf('%.2fs', $dtransfer)
	     . " $$file{LFN} => $$file{PFN}");

    # OK, got far enough to nuke and log it
    $self->relayDrop ($drop);
}

# Get a list of files to delete.
sub filesToDelete
{
    my ($self, $dbh, $limit, $node) = @_;

    my $now = &mytimeofday();

    # Find all the files that we are allowed to delete: 
    # Get the list of files from t_xfer_delete
    # We take the files oldest first.
    my @result;
    my %files = ();
    my $q = &dbexec($dbh,qq{
	select xd.fileid, xf.logical_name
	from t_xfer_delete xd
	left join t_xfer_file xf
	   on xd.fileid = xf.id
	where xd.time_complete is null and xd.node = :node
	and xf.logical_name is not null
	order by xd.time_request asc},
        ":node" => $$self{NODES_ID}{$node});
    while (my ($id, $lfn) = $q->fetchrow())
    {
	$files{$lfn} = $id;
	last if scalar keys %files >= $limit;
    }

    # Now get PFNs for all those files.
    my $pfns = &pfnLookup ([ keys %files ], $$self{PROTOCOL},
	    		   "local", $$self{STORAGEMAP});
    while (my ($lfn, $pfn) = each %$pfns)
    {
	do { &alert ("no pfn for $lfn"); next } if ! $pfn;
	push (@result, { LFN => $lfn, PFN => $pfn, FILEID => $files{$lfn},
			 NODEID => $$self{NODES_ID}{$node}, NODE => $node,
			 TIME_START => &mytimeofday() });
    }

    return @result;
}

# Create a drop for deleting a file.  We create a drop for ourselves,
# i.e. in our own inbox, and then process the file in "processDrop".
# This way we have a permanent record of where we are with deleting
# the file, in case we have to give up some operation for temporary
# failures.
sub startOne
{
    my ($self, $file) = @_;

    # Create a pending drop in my inbox
    my $drop = "$$self{DROPDIR}/inbox/$$file{FILEID}";
    do { &alert ("$drop already exists"); return 0; } if -d $drop;
    do { &alert ("failed to submit $$file{FILEID}"); &rmtree ($drop); return 0; }
	if (! &mkpath ($drop)
	    || ! &output ("$drop/packet", Dumper ($file))
	    || ! &touch ("$drop/go.pending"));

    # OK, kick it go
    &warn ("failed to mark $$file{FILEID} ready to go")
	if ! &mv ("$drop/go.pending", "$drop/go");

    return 1;
}

# Pick up work from the database.
sub idle
{
    my ($self, @pending) = @_;
    my $dbh = undef;
    my @nodes = ();

    eval
    {
	$dbh = $self->connectAgent();
	@nodes = $self->expandNodes();

	# Get a list of victims to evict.
	foreach my $node (@nodes)
	{
	    foreach my $file ($self->filesToDelete ($dbh, $$self{LIMIT}, $node))
	    {
		# If we are already processing this file, ignore it
		next if grep ($_ eq $$file{FILEID}, @pending);
		
		# Otherwise initiate destruction and doom
		$self->startOne ($file);
	    }
	    # Intermediate commit after having dealt with all files at a node
	    $dbh->commit();
	}
    };
    do { chomp ($@); &alert ("database error: $@");
	 eval { $dbh->rollback() } if $dbh } if $@;

    # Wait for all jobs to finish
    while (@{$$self{JOBS}})
    {
        $self->pumpJobs();
        select (undef, undef, undef, 0.1);
    }


    # Disconnect from the database
    $self->disconnectAgent();
}

1;
