#! /usr/bin/env perl

##H Clear up files marked for deletion.
##H
##H This agent perfoms the deletion requests entered into TMDB
##H by an administrator or via the web page.  Each request consists
##H of a dataset or block plus the node from where the file should
##H be deleted.
##H
##H Files on nodes without pending transfers are candidates for
##H clearing up.  Files are removed oldest first, first from the
##H TMDB replica table, then physically from disk.  File removal
##H may be a real removal, or simply ejection of the file from
##H stager disk but not permanently removed from tape.
##H
##H Usage:
##H   FileRemove
##H      -state DIRECTORY -nodes NAME [-ignode NAME] [-accept NAME]
##H      -db FILE[:SECTION] [-log OUT]
##H      -storagemap FILE [-protocol NAME] -delete COMMAND[,ARG...]
##H
##H -state         agent state directory.
##H -nodes         nodes to run and clean files for.
##H -ignore        comma-separated list of nodes to ignore transfers from
##H -accept        comma-separated list of nodes to accept transfers from
##H -db            database connection configuration parameter file.
##H -storagemap    storage mapping catalogue.
##H -protocol      protocol to use with storage map, by default "direct".
##H -delete        command to remove physical copy (typically FileDownloadDelete).
##H -log           where to redirect logging information.


BEGIN
{
  use warnings; use strict; $^W=1;
  our $me = $0; $me =~ s|.*/||;
  our $home = $0; $home =~ s|/[^/]+$||; $home ||= "."; $home .= "/../../Toolkit/Common";
  unshift(@INC, $home);
}


######################################################################
use Getopt::Long;
use UtilsHelp;

&GetOptions ("state=s"     => \$args{DROPDIR},
	     "log=s"       => \$args{LOGFILE},
	     "db=s"        => \$args{DBCONFIG},
             "nodes=s"     => sub { push(@{$args{NODES}}, split(/,/, $_[1])) },
             "ignore=s"    => sub { push(@{$args{IGNORE_NODES}}, split(/,/, $_[1])) },
             "accept=s"    => sub { push(@{$args{ACCEPT_NODES}}, split(/,/, $_[1])) },
             "delete=s"    => sub { push(@{$args{CMD_RM}}, split(/,/, $_[1])) },
	     "storagemap=s"=> \$args{STORAGEMAP},
	     "protocol=s"  => \$args{PROTOCOL},
	     "help|h"	   => sub { &usage() });

if (@ARGV || !$args{NODES} || !$args{DROPDIR} || !$args{DBCONFIG}
    || !$args{CMD_RM} || !$args{STORAGEMAP})
{
    die "Insufficient parameters, use -h for help.\n";
}

(new FileRemove (%args))->process();

################################################
package FileRemove; use strict; use warnings; use base 'UtilsAgent';
use File::Path;
use Data::Dumper;
use UtilsCommand;
use UtilsLogging;
use UtilsTiming;
use UtilsCatalogue;
use UtilsDB;
use DBI;
use UtilsRFIO;

sub new
{
    my $proto = shift;
    my $class = ref($proto) || $proto;
    my $self = $class->SUPER::new(@_);
    my %params = (DBCONFIG => undef,		# Database configuration file
	  	  NODES    => undef,		# Node names to run this agent for
	  	  IGNORE_NODES => ['%MSS'],	# TMDB nodes to ignore
	  	  ACCEPT_NODES => [],		# TMDB nodes to accept
		  WAITTIME => 60 + rand(15),	# Agent activity cycle
	  	  AGENTID  => "FileCleaner",    # Identity for activity logs
		  DELETING => undef,		# Are we deleting files now?
		  CMD_RM   => undef,            # cmd to remove physical files
		  PROTOCOL => 'direct',         # File access protocol
	  	  STORAGEMAP => undef		# Storage path mapping rules
		  );
    
    my %args = (@_);
    map { $$self{$_} = $args{$_} || $params{$_} } keys %params;
    bless $self, $class;
    return $self;
}

# Delete a file.  We do one step at a time; if the step fails, we just
# tell the caller to come back here again at a more opportune time.
# The steps are ordered such that they are safe to execute several
# times if we have to give for one reason or another.
sub deleteOneFile
{
    my ($self, $drop, $file) = @_;
    my $dbh = undef;
    my @nodes = ();

    my $status = eval {
	($dbh, @nodes) = &expandNodesAndConnect ($self);
	
	# Make sure the file is still safe to delete.  More transfers
	# out might have been created for this file while we were not
	# minding this particular file (sleeping or deleting things).
	my ($npending) = &dbexec($dbh, qq{
	    select count(fileid) from t_xfer_task xt
	       left join t_xfer_task_done xtd
	       on xt.id=xtd.task
	    where xt.from_node = :node
	    and xt.fileid = :fileid
	    and xtd.task is null},
	    ":node" => $$file{NODEID},
	    ":fileid" => $$file{FILEID})
	    ->fetchrow();
	if ($npending)
	{
	    &warn ("not removing $$file{LFN}, $npending pending transfers");
	    return 1;
	}
	
	# Now delete the replica entry to avoid new transfer edges.
	&dbexec($dbh, qq{
	    delete from t_xfer_replica where fileid = :fileid and node = :node},
	    ":fileid" => $$file{FILEID}, ":node" => $$file{NODEID});
	    
	# Issue file removal from disk now.
	my $log = "$$self{DROPDIR}/@{[time()]}.$$file{NODEID}.$$file{FILEID}.log";
	$self->addJob( sub { $self->deleteJob ($file, @_) },
		      { TIMEOUT => 30, LOGFILE => $log, DB => $dbh },
		      (@{$$self{CMD_RM}}, 'post', $$file{PFN}) );


	# Report completition time to DB. If the physicval deletion fails,
	# we will roll back.
	my $now = &mytimeofday();
	&dbexec($dbh, qq{
	    update t_xfer_delete set time_complete = :now
		where fileid = :fileid and node = :node},
		":fileid" => $$file{FILEID}, ":node" => $$file{NODEID}, ":now" => $now);
	return 1;
    };

    do { chomp ($@); &alert ("database error: $@");
	 eval { $dbh->rollback() } if $dbh;
	 $status = 0 } if $@;
    
    # Return status code to caller
    return $status;
}


sub deleteJob
{
    my ($self, $file, $job) = @_;
    if ($$job{STATUS})
    {
	&warn("failed to delete file $$file{PFN} at node "
	      ."$$file{NODE}, log in $$job{LOGFILE}");
    }
    else
    {
	&logmsg("deleted file $$file{PFN} at node $$file{NODE}");
	unlink ($$job{LOGFILE});
    }
}


sub processDrop
{
    my ($self, $drop) = @_;

    # Sanity checking
    return if (! $self->inspectDrop ($drop));
    delete $$self{BAD}{$drop};
    &timeStart($$self{STARTTIME});

    # Read back file information
    my $dropdir = "$$self{WORKDIR}/$drop";
    my $file = do { no strict "vars"; eval &input ("$dropdir/packet") };
    if ($@ || !$file || !$$file{FILEID} || !$$file{LFN} || !$$file{PFN} || !$$file{TIME_START})
    {
	&alert ("corrupt packet in $drop");
	$self->markBad ($drop);
	return;
    }

    # Try deleting this file.  If something fails, keep this drop as
    # is, we'll come back to it later.
    return if ! $self->deleteOneFile ($drop, $file);

    # Mark drop done so it will be nuked
    &touch ("$dropdir/done");

    # Log transfer delay stats
    my $dtransfer = &mytimeofday() - $$file{TIME_START};
    &logmsg ("xstats: $$file{NODE} " . sprintf('%.2fs', $dtransfer)
	     . " $$file{LFN} => $$file{PFN}");

    # OK, got far enough to nuke and log it
    $self->relayDrop ($drop);
}

# Get a list of files to delete.
sub filesToDelete
{
    my ($self, $dbh, $limit, $node, $filter, $filter_args) = @_;

    # Activate file level infos for files belonging to already
    # collapsed blocks
    my $now = &mytimeofday();
    &dbexec($dbh, qq{
	insert into t_dps_block_activate (BLOCK, TIME_REQUEST, TIME_UNTIL)
	select id, :now, :until from t_dps_block where id in
        (select xd.fileid from t_xfer_delete xd
           left join t_xfer_file xf on xf.id = xd.fileid
	where xf.id is null)},
	":now" => $now, ":until" => $now+24*3600*3);

    # Find all the files that we are allowed to delete: 
    # Get the list of files from t_xfer_delete
    # We take the files oldest first.
    my @result;
    my %files = ();
    my $q = &dbexec($dbh,qq{
	select xd.fileid, xf.logical_name
	from t_xfer_delete xd
	left join t_xfer_file xf
	   on xd.fileid = xf.id
	where xd.time_complete is null and xd.node = :node
	and xf.logical_name is not null ${$filter}
	order by xd.time_request asc},
        ":node" => $$self{NODES_ID}{$node}, %{$filter_args});
    while (my ($id, $lfn) = $q->fetchrow())
    {
	$files{$lfn} = $id;
	last if scalar keys %files >= $limit;
    }

    # Now get PFNs for all those files.
    my $pfns = &pfnLookup ([ keys %files ], $$self{PROTOCOL},
	    		   "local", $$self{STORAGEMAP});
    while (my ($lfn, $pfn) = each %$pfns)
    {
	do { &alert ("no pfn for $lfn"); next } if ! $pfn;
	push (@result, { LFN => $lfn, PFN => $pfn, FILEID => $files{$lfn},
			 NODEID => $$self{NODES_ID}{$node}, NODE => $node,
			 TIME_START => &mytimeofday() });
    }

    return @result;
}

# Create a drop for deleting a file.  We create a drop for ourselves,
# i.e. in our own inbox, and then process the file in "processDrop".
# This way we have a permanent record of where we are with deleting
# the file, in case we have to give up some operation for temporary
# failures.
sub startOne
{
    my ($self, $file) = @_;

    # Create a pending drop in my inbox
    my $drop = "$$self{DROPDIR}/inbox/$$file{FILEID}";
    do { &alert ("$drop already exists"); return 0; } if -d $drop;
    do { &alert ("failed to submit $$file{FILEID}"); &rmtree ($drop); return 0; }
	if (! &mkpath ($drop)
	    || ! &output ("$drop/packet", Dumper ($file))
	    || ! &touch ("$drop/go.pending"));

    # OK, kick it go
    &warn ("failed to mark $$file{FILEID} ready to go")
	if ! &mv ("$drop/go.pending", "$drop/go");

    return 1;
}

# Pick up work from the database.
sub idle
{
    my ($self, @pending) = @_;
    my $dbh = undef;
    my @nodes = ();

    eval
    {
	($dbh, @nodes) = &expandNodesAndConnect ($self);
	my ($filter, %filter_args) = &otherNodeFilter ($self, "xd.node");

	# Get a list of victims to evict.
	foreach my $node (@nodes)
	{
	    foreach my $file ($self->filesToDelete ($dbh, 100, $node, \$filter, \%filter_args))
	    {
		# If we are already processing this file, ignore it
		next if grep ($_ eq $$file{FILEID}, @pending);
		
		# Otherwise initiate destruction and doom
		$self->startOne ($file);
	    }
	    # Intermediate commit after having dalt with all files at a node
	    $dbh->commit();
	}
    };
    do { chomp ($@); &alert ("database error: $@");
	 eval { $dbh->rollback() } if $dbh } if $@;

    # Wait for all jobs to finish
    while (@{$$self{JOBS}})
    {
        $self->pumpJobs();
        select (undef, undef, undef, 0.1);
    }


    # Disconnect from the database
    &disconnectFromDatabase ($self, $dbh);

    # Have a little nap
    $self->nap ($$self{WAITTIME});
}

1;
