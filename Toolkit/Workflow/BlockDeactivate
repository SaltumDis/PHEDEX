#! /usr/bin/env perl

##H Deactivate blocks when file-level information is not needed.
##H
##H Files belong to blocks; when the file replicas are no longer needed
##H they are collapsed to block replicas, which remember entire sets of
##H files.  This agent monitors completeness of blocks and triggers
##H removal of the file-level information in TMDB.
##H
##H Usage:
##H   BlockDeactivate
##H      -state DIRECTORY -node NAME -db FILE[:SECTION] [-wait SECS]
##H      [-holdoff SECS]
##H
##H -state     agent state directory
##H -node      the node where this agent runs
##H -db        database connection configuration parameter file
##H -wait      time to wait in seconds between work scans
##H -holdoff   time lag in seconds before deactivation (default: 3 days)

BEGIN {
  use warnings; use strict; $^W=1;
  our $me = $0; $me =~ s|.*/||;
  our $home = $0; $home =~ s|/[^/]+$||; $home ||= "."; $home .= "/../../Toolkit/Common";
  unshift(@INC, $home);
}

######################################################################
use UtilsHelp;
use Getopt::Long;
my %args = (WAITTIME => 60);
&GetOptions ("state=s"		=> \$args{DROPDIR},
	     "node=s"           => \$args{MYNODE},
	     "db=s"		=> \$args{DBCONFIG},
	     "wait=f"		=> \$args{WAITTIME},
	     "help|h"		=> sub { &usage() },
	     "holdoff=f"        => \$args{HOLDOFF});

if (@ARGV || !$args{MYNODE} || !$args{DROPDIR} || !$args{DBCONFIG})
{
    die "Insufficient parameters, use -h for help.\n";
}

(new BlockDeactivate (%args))->process();

################################################
package BlockDeactivate; use strict; use warnings; use base 'UtilsAgent';
use UtilsLogging;
use UtilsTiming;
use UtilsDB;

sub new
{
    my $proto = shift;
    my $class = ref($proto) || $proto;
    my $self = $class->SUPER::new(@_);
    my %params = (DBCONFIG => undef,		# Database configuration file
		  MYNODE => undef,		# My TMDB node
	  	  AGENTID => "BlockDeactivate", # Identity for activity logs
		  HOLDOFF => 3*86400);          # Hold-off time for pruning

    my %args = (@_);
    map { $self->{$_} = $args{$_} || $params{$_} } keys %params;
    bless $self, $class;
    return $self;
}

# Run the main loop of this agent.
sub idle
{
    my ($self, @pending) = @_;
    my $dbh = undef;
    eval
    {
	$dbh = &connectToDatabase ($self) or die "failed to connect";

	# Deactivate complete blocks.  Get all blocks whose all replicas
	# are complete and active.  First of all, ignore blocks where
	# replicas are already inactive.  Secondly, ignore all blocks
	# which have been touched "recently" to allow things to settle.
	#
	# Consider remaining (active) block replicas.  If all replicas
	# of a block have as many files as the block has, deactive the
	# block (and all replicas).  We also require for extra safety
	# that there can be no files in transfer, or export.
	#
	# We do *not* require that dest_files = node_files, as at file
	# source nodes dest_files is usually zero, and we still want
	# to deactivate.  This should be safe -- we deactivate a block
	# on intermediate node is if a) the entire block has already
	# reached all current destination nodes, b) either all or none
	# of the files have been removed in intermediate nodes, and
	# c) several days has passed like this.  It is unlikely that
	# all these criteria will be met simultaneously, and in any
	# case the fix is easy: reactivate the block.
	#
	# Process just one block each time to avoid overloading undo
	# segments unncessarily.  We can always deactive more later.
	#
	# What we do here requires a great degree of transactinal
	# consistency.  For one, we don't want someone to open blocks
	# while we are deactiving them; this is prevented by locking
	# the t_block row with "select .. for update" while operating
	# on it, a convention followed by all programs which modify
	# block table.  Secondly, we need to make sure we delete the
	# exact number of file replicas we expected to delete; if an
	# inconsitency is detected, we roll back the transcation.
	my $qblocks = &dbexec ($dbh, qq{
	    select b.name
	    from t_block b
	    where b.isopen = 0
	      and b.timestamp < :limit
      	      and exists (select name from t_block_replica br
      		          where br.name = b.name)
	      and (b.files, b.bytes, 0, 0, 1, 1) = all
      	          (select br.node_files, br.node_bytes, br.xfer_files,
		  	  br.expt_files, sign(:limit - br.last_update),
			  br.isactive
		   from t_block_replica br
		   where br.name = b.name)
	    order by b.files desc
	    for update of b.name},
	    ":limit" => &mytimeofday () - $self->{HOLDOFF});
        if (my ($name) = $qblocks->fetchrow())
	{
	    # Get the number of file replicas expected to delete.
	    my ($nfr) = &dbexec ($dbh, qq{
		select sum(node_files)
		from t_block_replica
		where name = :block},
		":block" => $name)
		->fetchrow();
	    if (! $nfr)
	    {
		&alert ("refusing to deactivate block $name with no files");
		next;
	    }

	    # Deactivate.
	    my ($dr, $nr) = &dbexec ($dbh, qq{
		delete from t_replica_state where guid in
		(select guid from t_file where inblock = :block)},
		":block" => $name);

	    if ($nr != $nfr)
	    {
		&alert ("deactivating $name deleted $nr file replicas,"
			. " expected to delete $nfr, undoing deactivation");
		$dbh->rollback ();
	    }
	    else
	    {
	        my ($db, $nb) = &dbexec ($dbh, qq{
		    update t_block_replica set isactive = 0 where name = :block},
		    ":block" => $name);
	        &logmsg ("deactivated $name: $nr file replicas, $nb block replicas");
	        $dbh->commit ();
	    }
        }
    };
    do { chomp ($@); &alert ("database error: $@");
	 eval { $dbh->rollback() } if $dbh } if $@;

    # Disconnect from the database
    &disconnectFromDatabase ($self, $dbh);

    # Have a little nap
    $self->nap ($self->{WAITTIME});
}

1;
