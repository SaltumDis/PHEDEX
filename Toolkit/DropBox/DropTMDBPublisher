#!/usr/bin/env perl

## This agent registers files into the transfer database as new
## unallocated files.  This will cause the file allocation and
## configuration agents to allocate the files to the tiers.  The
## files must already be registered in the local catalogue and
## the PFNs must be correct full paths for this site.
##
## Each drop is required to have a XML catalogue fragment and a
## checksum file.  The information must be new: the files must be
## unknown to the transfer database.  All the file information is
## inserted, including all the meta data, but no allocation is
## given as destinations.
##
## Like all drop box agents, this agent periodically inspects the
## inbox, looking for new completed drops.  For each new drop, the
## files are inserted into the database, and the drop is passed to
## the next drop box agent, or destroyed if there is no next agent.
## If the agent terminates, crashes or is otherwise stopped, it will
## pick up in the right place when started the next time.

BEGIN {
  use strict; use warnings;
  our $me = $0; $me =~ s|.*/||;
  our $home = $0; $home =~ s|/[^/]+$||; $home ||= "."; $home .= "/../../Toolkit/Common";
  unshift(@INC, $home);
}

######################################################################
my %args = (VERSION => 2, DBITYPE => "Oracle");
while (scalar @ARGV)
{
    if ($ARGV[0] eq '-db' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DBNAME} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-dbi' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DBITYPE} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-dbuser' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DBUSER} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-dbpass' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DBPASS} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-node' && scalar @ARGV > 1)
    { shift (@ARGV); $args{MYNODE} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-in' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DROPDIR} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-out' && scalar @ARGV > 1)
    { shift (@ARGV); push (@{$args{NEXTDIR}}, shift(@ARGV)); }
    elsif ($ARGV[0] eq '-wait' && scalar @ARGV > 1)
    { shift (@ARGV); $args{WAITTIME} = shift(@ARGV); }
    else
    { last; }
}
	
if (scalar @ARGV || !$args{DROPDIR}
    || ! defined $args{DBNAME} || !$args{DBUSER}
    || !$args{DBPASS} || !$args{DBITYPE}
    || !$args{MYNODE})
{
    print STDERR
	"usage: $me -node NODE-NAME -in IN-DROP-BOX [-out NEXT-DROP-BOX]\n",
	"    -db NAME -dbuser USER -dbpass PASSWORD [-dbitype TYPE]\n",
	"    [-wait SECS-TO-WAIT]\n";
    exit (1);
}

(new DropTMDBPublisher (%args))->process();

######################################################################
# Routines specific to this agent.
package DropTMDBPublisher; use strict; use warnings; use base 'UtilsAgent';
use File::Path;
use UtilsCommand;
use UtilsLogging;
use UtilsTiming;
use UtilsReaders;
use UtilsDB;

sub new
{
    my $proto = shift;
    my $class = ref($proto) || $proto;
    my $self = $class->SUPER::new(@_);
    my %params = (DBITYPE => undef,		# Database driver binding
    		  DBNAME => undef,		# Database name
	  	  DBUSER => undef,		# Database user name
	  	  DBPASS => undef,		# Database user password
	  	  MYNODE => undef);		# My TMDB node name
    my %args = (@_);
    map { $self->{$_} = $args{$_} || $params{$_} } keys %params;
    bless $self, $class;
    return $self;
}

# Update TMDB for each file mentioned.
sub updateTransferDatabase
{
    my ($self, $drop, $nodes, $attrs) = @_;

    my $dbh;
    eval
    {
        # Update database
	$dbh = &connectToDatabase ($self, 0) or die "failed to connect";

        my $now = &mytimeofday ();
	my $fstmt = &dbprep ($dbh, qq{
	    insert into t_file
	    (timestamp, guid, node, inblock, insubblock,
	     lfn, filetype, filesize, checksum)
	    values (:now, :guid, :node, :inblock, :insubblock,
	     :lfn, :filetype, :filesize, :checksum)});
    	my $rstmt = &dbprep ($dbh, qq{
	    insert into t_replica_state (timestamp, guid, node, state, state_timestamp)
	    values (:now, :guid, :node, 0, :now)});
    	my $mstmt = &dbprep ($dbh, qq{
	    insert into t_file_attributes (guid, attribute, value)
	    values (:guid, :attr, :value)});
    	my $bqstmt = &dbprep ($dbh, qq{
	    select count(*) from t_block where name = :block});
    	my $bistmt = &dbprep ($dbh, qq{
	    insert into t_block (name, owner, dataset, files, bytes)
	    values (:name, :owner, :dataset, -1, -1)});

        foreach my $f (@$attrs)
	{
	    # Check the file block is known, otherwise create an open one
	    my $block = "$f->{META}{owner}/$f->{META}{dataset}";
	    &dbbindexec ($bqstmt, ":block" => $block);
	    my ($found) = $bqstmt->fetchrow();
	    $bqstmt->finish();

	    &dbbindexec ($bistmt,
			 ":name" => $block,
			 ":owner" => $f->{META}{owner},
			 ":dataset" => $f->{META}{dataset})
		if ! $found;

	    # Insert file information
	    &dbbindexec ($fstmt,
		         ":now" => $now,
			 ":guid" => $f->{GUID},
		         ":node" => $self->{MYNODE},
			 ":inblock" => "$f->{META}{owner}/$f->{META}{dataset}",
			 ":insubblock" => "$f->{META}{jobid}",
			 ":lfn" => $f->{LFN}[0],
			 ":filetype" => $f->{PFN}[0]{TYPE},
			 ":filesize" => $f->{FILESIZE},
		 	 ":checksum" => $f->{CHECKSUM});

	    # Insert file attribute data
            my %meta = (map { "POOL_".$_ => $f->{META}{$_} } keys %{$f->{META}});
	    foreach my $m (sort keys %meta)
	    {
	        &dbbindexec ($mstmt,
			     ":guid" => $f->{GUID},
			     ":attr" => $m,
			     ":value" => $meta{$m});
	    }

	    # Insert replica for all nodes
	    foreach my $n (@$nodes)
	    {
	        &dbbindexec ($rstmt, 
	    	             ":now" => $now,
			     ":guid" => $f->{GUID},
			     ":node" => $n);
	    }
	}

	$dbh->commit () if $dbh;
	&disconnectFromDatabase ($self, $dbh);
    };

    if ($@)
    {
	&alert("$drop: database failure: $@");
	if ($dbh)
	{
	    $dbh->rollback();
	    &disconnectFromDatabase ($self, $dbh);
	}
	$self->markBad ($drop);
	return 1;
    }

    &touch("$self->{WORKDIR}/$drop/done");
    return 0;
}

# Actually process the drop.
sub processDrop
{
    my ($self, $drop) = @_;

    # Sanity checking
    return if (! $self->inspectDrop ($drop));
    delete $self->{BAD}{$drop};
    &timeStart($self->{STARTTIME});

    # Prepare attribute cache for this drop
    my $dropdir = "$self->{WORKDIR}/$drop";

    # Read catalogue data and build attribute cache from it
    my $xmlcat = (<$dropdir/XMLCatFragment.*.{txt,xml}>)[0];
    do { &alert("$drop: no catalogue"); $self->markBad ($drop); return }
        if ! $xmlcat;
    my $catalogue = eval { &readXMLCatalogue ($xmlcat) };
    do { &alert ("$drop: $@"); $self->markBad ($drop); return } if $@;

    # Merge checksum data into cache
    my $cksumfile = (<$dropdir/Checksum.*.txt>)[0];
    do { &alert ("no checksum file in $drop"); $self->markBad ($drop); return }
        if ! $cksumfile;

    my @cksums = eval { &readChecksumData ($cksumfile) };
    do { &alert ($@); $self->markBad ($drop); return } if $@;

    my $bad = 0;
    foreach my $file (@$catalogue)
    {
	my $lfn = $file->{LFN}[0];
	my $cksum = (grep ($_->[2] eq $lfn, @cksums))[0];

	if (! $cksum)
	{
	   &alert ("$drop: no checksum for $lfn");
	   $bad = 1;
	   next;
        }

	&warn ("$drop: zero-size file $lfn") if ! $cksum->[1];

	$file->{CHECKSUM} = $cksum->[0];
	$file->{FILESIZE} = $cksum->[1];
    }

    do { $self->markBad ($drop); return } if $bad;

    # Get node list
    my @nodes = split(/\s+/, &input ("$dropdir/PhEDEx-Nodes.txt") || $self->{MYNODE});

    # Insert information to the database.
    return if $self->updateTransferDatabase ($drop, \@nodes, $catalogue);
    $self->relayDrop ($drop);
    &logmsg("stats: $drop @{[&formatElapsedTime($self->{STARTTIME})]} success");
}
