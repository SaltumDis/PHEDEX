#!/usr/bin/perl

## This agent registers files into the transfer database as new
## unallocated files.  This will cause the file allocation and
## configuration agents to allocate the files to the tiers.  The
## files must already be registered in the local catalogue and
## the PFNs must be correct full paths for this site.
##
## Each drop is required to have a XML catalogue fragment and a
## checksum file.  The information must be new: the files must be
## unknown to the transfer database.  All the file information is
## inserted, including all the meta data, but no allocation is
## given as destinations.
##
## Like all drop box agents, this agent periodically inspects the
## inbox, looking for new completed drops.  For each new drop, the
## files are inserted into the database, and the drop is passed to
## the next drop box agent, or destroyed if there is no next agent.
## If the agent terminates, crashes or is otherwise stopped, it will
## pick up in the right place when started the next time.

BEGIN { use strict; use warnings; }
$me = $0; $me =~ s|.*/||;
$home = $0; $home =~ s|/[^/]+$||; $home ||= ".";
unshift(@INC, $home);

######################################################################
my %args = (VERSION => 2, DBITYPE => "Oracle");
while (scalar @ARGV)
{
    if ($ARGV[0] eq '-db' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DBNAME} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-dbi' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DBITYPE} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-dbuser' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DBUSER} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-dbpass' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DBPASS} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-node' && scalar @ARGV > 1)
    { shift (@ARGV); $args{MYNODE} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-in' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DROPDIR} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-out' && scalar @ARGV > 1)
    { shift (@ARGV); push (@{$args{NEXTDIR}}, shift(@ARGV)); }
    elsif ($ARGV[0] eq '-wait' && scalar @ARGV > 1)
    { shift (@ARGV); $args{WAITTIME} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-version' && scalar @ARGV > 1)
    { shift (@ARGV); $args{VERSION} = shift(@ARGV); }
    else
    { last; }
}
	
if (scalar @ARGV || !$args{DROPDIR}
    || !$args{DBNAME} || !$args{DBUSER}
    || !$args{DBPASS} || !$args{DBITYPE}
    || ($args{VERSION} ne 1 && $args{VERSION} ne 2)
    || ($args{VERSION} eq 2 && !$args{MYNODE}))
{
    print STDERR
	"usage: $me -node NODE-NAME\n",
	"    -db NAME -dbuser USER -dbpass PASSWORD [-dbitype TYPE]\n",
	"    -in IN-DROP-BOX [-out NEXT-DROP-BOX]\n",
	"    [-version 1/2] [-wait SECS-TO-WAIT]\n";
    exit (1);
}

(new DropTMDBPublisher (%args))->process();

######################################################################
# Routines specific to this agent.
package DropTMDBPublisher; use strict; use warnings; use base 'UtilsAgent';
use File::Path;
use UtilsCommand;
use UtilsLogging;
use UtilsTiming;
use UtilsCache;

sub new
{
    my $proto = shift;
    my $class = ref($proto) || $proto;
    my $self = $class->SUPER::new(@_);
    my %params = (DBITYPE => undef,		# Database driver binding
    		  DBNAME => undef,		# Database name
	  	  DBUSER => undef,		# Database user name
	  	  DBPASS => undef,		# Database user password
	  	  MYNODE => undef,		# My TMDB node name
	  	  VERSION => 2);		# TMDB version
    my %args = (@_);
    map { $self->{$_} = $args{$_} || $params{$_} } keys %params;
    bless $self, $class;
    return $self;
}

# Update TMDB for each file mentioned.
sub updateTransferDatabase
{
    my ($self, $drop, $attrs) = @_;

    my $dbh;
    eval
    {
        # Update database
        if ($self->{DBNAME})
	{
	    eval "use DBI"; die $@ if $@; # Allow testing without DBI
	    $dbh = DBI->connect ("DBI:$self->{DBITYPE}:$self->{DBNAME}",
		    		 $self->{DBUSER}, $self->{DBPASS},
				 { RaiseError => 1, AutoCommit => 0 });
	}

        foreach my $f (@$attrs)
	{
	    my @cmds;
	    my $now = time();
            my %meta = ('filesize' => $f->{FILESIZE},
		        (exists $f->{CHECKSUM} ? ('checksum' => $f->{CHECKSUM}) : ()),
			map { "POOL_".$_ => $f->{META}{$_} } keys %{$f->{META}});

	    if ($self->{VERSION} == 1) {
		@cmds = ("insert into filesfortransfer "
			 ." (guid,castor_filename,entry_time,checksum,filesize,allocated_to_t1s,filegroup)"
		         ."values ('$f->{GUID}', '$f->{PFN}', $now, $f->{CHECKSUM},"
		         ."  $f->{FILESIZE}, '1', '@{[$f->{META}{'jobid'} || '']}')");
	    } elsif ($self->{VERSION} == 2) {
		@cmds = ("insert into t_files_for_transfer"
			 ." values ('$f->{GUID}', '$self->{MYNODE}')",
			 "insert into t_replica_state"
			 ." values ('$f->{GUID}', '$self->{MYNODE}', $now, 0, $now, 0, $now)",
	       		 map { "insert into t_replica_metadata"
		     		." values ('$f->{GUID}', '$_', '$meta{$_}')" }
		 	 sort keys %meta);
	    }

	    foreach my $cmd (@cmds)
	    {
		if ($self->{DBNAME}) {
		    $dbh->do ($cmd);
	        } else {
		    &logmsg ($cmd);
		}
	    }
	}

	$dbh->commit() if $dbh;
	$dbh->disconnect() if $dbh;
    };

    if ($@)
    {
	&alert("$drop: database failure: $@");
	if ($dbh)
	{
	    $dbh->rollback();
	    $dbh->disconnect();
	}
	$self->markBad ($drop);
	return 1;
    }

    &touch("$self->{WORKDIR}/$drop/done");
    return 0;
}

# Actually process the drop.
sub processDrop
{
    my ($self, $drop) = @_;

    # Sanity checking
    return if (! $self->inspectDrop ($drop));
    delete $self->{BAD}{$drop};
    &timeStart($self->{STARTTIME});

    # Read attribute cache, this is all we use
    my $dropdir = "$self->{WORKDIR}/$drop";
    my $attrs = eval { &readAttributeCache ("$dropdir/attrs") };
    do { &alert ("$drop: $@"); $self->markBad ($drop); return; } if $@;

    # Insert information to the database.
    return if $self->updateTransferDatabase ($drop, $attrs);
    $self->relayDrop ($drop);
    &logmsg("stats: $drop @{[&formatElapsedTime($self->{STARTTIME})]} success");
}
