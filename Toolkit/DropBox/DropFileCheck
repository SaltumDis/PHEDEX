#!/usr/bin/env perl

##H Verify PFNs mentioned in the drops exist and have non-zero size.
##H
##H Usage:
##H   DropFileCheck
##H      -state DIRECTORY [-next DIRECTORY] [-wait SECS]
##H
##H -state     agent state directory, including inbox
##H -next      next agent to pass the drops to; can be given several times
##H -wait      time to wait in seconds between work scans

BEGIN {
  use strict; use warnings; $^W=1;
  our $me = $0; $me =~ s|.*/||;
  our $home = $0; $home =~ s|/[^/]+$||; $home ||= "."; $home .= "/../../Toolkit/Common";
  unshift(@INC, $home);
}

######################################################################
use UtilsHelp;
my %args;
while (scalar @ARGV)
{
    if ($ARGV[0] eq '-state' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DROPDIR} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-next' && scalar @ARGV > 1)
    { shift (@ARGV); push (@{$args{NEXTDIR}}, shift(@ARGV)); }
    elsif ($ARGV[0] eq '-wait' && scalar @ARGV > 1)
    { shift (@ARGV); $args{WAITTIME} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-h')
    { &usage(); }
    else
    { last; }
}
	
if (@ARGV || !$args{DROPDIR})
{
    die "Insufficient parameters, use -h for help.\n";
}

(new DropFileCheck (%args))->process();

######################################################################
# Routines specific to this agent.
package DropFileCheck; use strict; use base 'UtilsAgent';
use File::Path;
use UtilsCache;
use UtilsCommand;
use UtilsLogging;
use UtilsTiming;
use UtilsReaders;

sub new
{
    my $proto = shift;
    my $class = ref($proto) || $proto;
    my $self = $class->SUPER::new(@_);
    my %params = ();
    my %args = (@_);
    map { $self->{$_} = $args{$_} || $params{$_} } keys %params;
    bless $self, $class;
    return $self;
}

# Really process the drop.  Only done in leaf processes: either the
# main agent if it has no workers, or in its slaves.
sub processDrop
{
    my ($self, $drop) = @_;

    # Sanity checking
    return if (! $self->inspectDrop ($drop));
    delete $self->{BAD}{$drop};
    &timeStart($self->{STARTTIME});

    # Read catalogue data and build attribute cache from it
    my $dropdir = "$self->{WORKDIR}/$drop";
    my $xmlcat = (<$dropdir/XMLCatFragment.*.{txt,xml}>)[0];
    do { &alert("$drop: no catalogue"); $self->markBad ($drop); return }
        if ! $xmlcat;
    my $catalogue = eval { &readXMLCatalogue ($xmlcat) };
    do { chomp ($@); &alert ("$drop: $@"); $self->markBad ($drop); return } if $@;

    # Check that the files exist and get their size
    my $bad = 0;
    foreach my $file (@$catalogue)
    {
	my $pfn = $file->{PFN}[0]{PFN};
	my ($dev, $ino, $mode, $nlink, $uid, $gid, $rdev, $size,
	    $atime, $mtime, $ctime, $blksize, $blocks) = stat($pfn);

	# Check that we like this file
	if (! defined $size)
	{
	    &alert ("$drop: cowardly refusing to transfer non-existent file $pfn");
	    $bad = 1;
	}
	elsif (! $size)
	{
	    &warn ("$drop: zero-size file $pfn");
	}
    }

    # Mark drop accordingly
    if ($bad)
    {
        $self->markBad ($drop);
    }
    else
    {
        &touch ("$dropdir/done");
        $self->relayDrop ($drop);
        &logmsg("stats: $drop @{[&formatElapsedTime($self->{STARTTIME})]} succcess");
    }
}
