#!/usr/bin/env perl

##H Update file size statistics for faster web access.
##H
##H Usage:
##H   InfoFileSize
##H      -state DIRECTORY -db FILE[:SECTION] [-wait SECS]
##H
##H -state     agent state directory
##H -db        database connection configuration parameter file
##H -wait      time to wait in seconds between work scans

BEGIN {
  use strict; use warnings;
  our $me = $0; $me =~ s|.*/||;
  our $home = $0; $home =~ s|/[^/]+$||; $home ||= "."; $home .= "/../../Toolkit/Common";
  unshift(@INC, $home);
}

######################################################################
use UtilsHelp;
my %args = (WAITTIME => 600);
while (scalar @ARGV)
{
    if ($ARGV[0] eq '-wait' && scalar @ARGV > 1)
    { shift (@ARGV); $args{WAITTIME} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-state' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DROPDIR} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-db' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DBCONFIG} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-h')
    { &usage(); }
    else
    { last; }
}

if (@ARGV || !$args{DROPDIR} || !$args{DBCONFIG})
{
    die "Insufficient parameters, use -h for help.\n";
}

(new InfoFileSize (%args))->process();

######################################################################
# Routines for this agent.
package InfoFileSize; use strict; use warnings; use base 'UtilsAgent';
use UtilsCommand;
use UtilsLogging;
use UtilsTiming;
use UtilsCatalogue;
use UtilsDB;

sub new
{
    my $proto = shift;
    my $class = ref($proto) || $proto;
    my $self = $class->SUPER::new(@_);
    my %params = (DBCONFIG => undef);		# Database configuration file
    my %args = (@_);
    map { $self->{$_} = $args{$_} || $params{$_} } keys %params;
    bless $self, $class;
    return $self;
}

# Called by agent main routine before sleeping.  Update database.
sub idle
{
    my ($self, @pending) = @_;
    my $dbh = undef;
    eval
    {
	$dbh = &connectToDatabase ($self, 0) or die "failed to connect";
        # FIXME: Pick up and process messages to me

	# Recreate statistics
	my $now = &mytimeofday();
	&dbexec($dbh, qq{delete from t_info_file_size_overview});
	&dbexec($dbh, qq{delete from t_info_file_size_histogram});
	&dbexec($dbh, qq{insert into t_info_file_size_overview
			(select
			   :now,
			   nvl(count(filesize),0),
		           nvl(sum(filesize),0),
		           nvl(min(filesize),0),
		           nvl(max(filesize),0),
	                   nvl(avg(filesize),0),
		           nvl(percentile_disc(.5) within group (order by filesize),0)
			 from t_file)},
	        ":now" => $now);
	&dbexec($dbh, qq{insert into t_info_file_size_histogram
			(select
			   :now,
			   binsize,
			   10 * 1024 * 1024,
			   count(binsize),
			   sum(realsize)
			 from (select
			         trunc(filesize/(10 * 1024 * 1024)) as binsize,
				 filesize as realsize
			       from t_file)
			 group by binsize)},
	        ":now" => $now);

	$dbh->commit();
    };
    do { &alert ("database error: $@");
	 eval { $dbh->rollback() } if $dbh; } if $@;

    # Disconnect from the database
    &disconnectFromDatabase ($self, $dbh);

    # Check children are still running and then wait
    $self->nap ($self->{WAITTIME});
}
