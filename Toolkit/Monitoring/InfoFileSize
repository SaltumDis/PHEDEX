#!/usr/bin/env perl

##H Update file size statistics for faster web access.
##H
##H Usage:
##H   InfoFileSize -state DIRECTORY -db FILE[:SECTION] -node NODE [-log OUT]
##H
##H -state     agent state directory
##H -db        database connection configuration parameter file
##H -node      the node where this agent runs
##H -log       where to redirect logging information

######################################################################
my %args;
use Getopt::Long;
use PHEDEX::Core::Help;
&GetOptions ("state=s"     => \$args{DROPDIR},
	     "log=s"       => \$args{LOGFILE},
             "db=s"        => \$args{DBCONFIG},
             "node=s"      => \$args{MYNODE},
	     "help|h"      => sub { &usage() });

if (@ARGV || !$args{DROPDIR} || !$args{DBCONFIG} || !$args{MYNODE})
{
    die "Insufficient parameters, use -h for help.\n";
}

(new InfoFileSize (%args))->process();

######################################################################
# Routines for this agent.
package InfoFileSize; use strict; use warnings; use base 'PHEDEX::Core::Agent';
use PHEDEX::Core::Logging;
use PHEDEX::Core::Timing;
use PHEDEX::Core::DB;

sub new
{
    my $proto = shift;
    my $class = ref($proto) || $proto;
    my $self = $class->SUPER::new(@_);
    my %params = (DBCONFIG => undef,		# Database configuration file
		  MYNODE => undef,		# My TMDB node name
	    	  WAITTIME => 1200 + rand(200));# Agent activity cycle
    my %args = (@_);
    map { $$self{$_} = $args{$_} || $params{$_} } keys %params;
    bless $self, $class;
    return $self;
}

# Called by agent main routine before sleeping.  Update database.
sub idle
{
    my ($self, @pending) = @_;
    my $dbh = undef;
    eval
    {
	$dbh = &connectToDatabase ($self);

	# Recreate statistics
	my $now = &mytimeofday();
	&dbexec($dbh, qq{delete from t_status_file_size_overview});
	&dbexec($dbh, qq{delete from t_status_file_size_histogram});
	&dbexec($dbh, qq{insert into t_status_file_size_overview
			(select
			   :now,
			   nvl(count(filesize),0),
		           nvl(sum(filesize),0),
		           nvl(min(filesize),0),
		           nvl(max(filesize),0),
	                   nvl(avg(filesize),0),
		           nvl(percentile_disc(.5) within group (order by filesize),0)
			 from t_dps_file)},
	        ":now" => $now);
	&dbexec($dbh, qq{insert into t_status_file_size_histogram
			(select
			   :now,
			   binsize,
			   10 * 1024 * 1024,
			   count(binsize),
			   sum(realsize)
			 from (select
			         trunc(filesize/(10 * 1024 * 1024)) as binsize,
				 filesize as realsize
			       from t_dps_file)
			 group by binsize)},
	        ":now" => $now);

	$dbh->commit();
    };
    do { chomp ($@); &alert ("database error: $@");
	 eval { $dbh->rollback() } if $dbh; } if $@;

    # Disconnect from the database
    &disconnectFromDatabase ($self, $dbh);

    # Check children are still running and then wait
    $self->nap ($$self{WAITTIME});
}
