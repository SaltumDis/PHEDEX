#!/usr/bin/env perl

##H Clean up active tables by moving away historical and timed-out data.
##H
##H Usage:
##H   TableCleaner
##H      -state DIRECTORY -db FILE[:SECTION] [-wait SECS]
##H
##H -state     agent state directory
##H -db        database connection configuration parameter file
##H -wait      time to wait in seconds between work scans

BEGIN {
  use strict; use warnings; $^W=1;
  our $me = $0; $me =~ s|.*/||;
  our $home = $0; $home =~ s|/[^/]+$||; $home ||= "."; $home .= "/../../Toolkit/Common";
  unshift(@INC, $home);
}

######################################################################
use UtilsHelp;
my %args = (WAITTIME => 7);
while (scalar @ARGV)
{
    if ($ARGV[0] eq '-wait' && scalar @ARGV > 1)
    { shift (@ARGV); $args{WAITTIME} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-state' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DROPDIR} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-db' && scalar @ARGV > 1)
    { shift (@ARGV); $args{DBCONFIG} = shift(@ARGV); }
    elsif ($ARGV[0] eq '-h')
    { &usage(); }
    else
    { last; }
}

if (@ARGV || !$args{DROPDIR} || !$args{DBCONFIG})
{
    die "Insufficient parameters, use -h for help.\n";
}

(new TableCleaner (%args))->process();

######################################################################
# Routines for this agent.
package TableCleaner; use strict; use warnings; use base 'UtilsAgent';
use UtilsCommand;
use UtilsLogging;
use UtilsTiming;
use UtilsCatalogue;
use UtilsDB;

sub new
{
    my $proto = shift;
    my $class = ref($proto) || $proto;
    my $self = $class->SUPER::new(@_);
    my %params = (DBCONFIG => undef);		# Database configuration file
    my %args = (@_);
    map { $self->{$_} = $args{$_} || $params{$_} } keys %params;
    bless $self, $class;
    return $self;
}

# Called by agent main routine before sleeping.  Update database.
sub idle
{
    my ($self, @pending) = @_;
    my $dbh = undef;
    eval
    {
	$dbh = &connectToDatabase ($self, 0) or die "failed to connect";

	# Archive completed transfers from t_transfer_state into
	# t_transfer_completed; completed is to_state=3.
	my $rows = 0;
	my $archive = &dbprep ($dbh, qq{
	    insert into t_transfer_completed values
	    (:timestamp, :guid, :to_node, :to_state, :to_timestamp,
	     :from_node, :from_state, :from_timestamp)});
	my $retire = &dbprep ($dbh, qq{
	    delete from t_transfer_state
	    where guid = :guid and to_node = :to_node});
	my $completed = &dbexec ($dbh, qq{
	    select timestamp, guid, to_node, to_state, to_timestamp,
	           from_node, from_state, from_timestamp
	    from t_transfer_state
	    where to_state = 3});
        while (my $row = $completed->fetchrow_hashref())
	{
	    &dbbindexec ($archive, map { (":$_" => $row->{$_}) } keys %$row);
	    &dbbindexec ($retire, ":guid" => $row->{GUID},
		         ":to_node" => $row->{TO_NODE});
	    $dbh->commit();
	    ++$rows;
	}

	&logmsg ("$rows completed transfers archived") if $rows;

	# Delete obsolete agent status messages (3 days)
	my %old = (":old" => &mytimeofday() - 3*86400);
	&dbexec($dbh,qq{delete from t_agent_status where timestamp < :old}, %old);
	&dbexec($dbh,qq{delete from t_info_agent_status where timestamp < :old}, %old);
	$dbh->commit ();

	# Downgrade obsolete "wanted" or error files (one day)
	my $now = &mytimeofday();
	&dbexec($dbh, qq{
	    update t_transfer_state
	    set to_state = 0, to_timestamp = :now
	    where (to_state = 1 or to_state >= 100)
	      and to_timestamp < :old},
	    ":now" => $now, ":old" => $now - 86400);
	$dbh->commit ();

    	# And really obsolete transfers (one week)
	&dbexec($dbh, qq{
	    update t_transfer_state
	    set to_state = 0, to_timestamp = :now
	    where to_state = 2 and to_timestamp < :old},
	    ":now" => $now, ":old" => $now - 7*86400);
	$dbh->commit ();
    };
    do { chomp ($@); &alert ("database error: $@");
	 eval { $dbh->rollback() } if $dbh; } if $@;

    # Disconnect from the database
    &disconnectFromDatabase ($self, $dbh);

    # Check children are still running and then wait
    $self->nap ($self->{WAITTIME});
}
