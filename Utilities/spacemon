#!/usr/bin/env perl
#
# spacemon - client side utility for CMS Space Monitoring
# 
# Intended functionality: 
#  * Parces and aggregates storage dump file 
#  * Creates a record object for space monitoring 
#  * Downloads TFC and CMS namespace conventions
#  * Maps directories to CMS data types 
#  * Uploads record to dmwmmon database, or saves to a local file for later upload
#    (default: print to stdout)
#
# See project page for details:
#            https://twiki.cern.ch/twiki/bin/view/CMSPublic/CompProjSpaceMon
#
############################################################################

use strict;
use warnings;
use Carp;
use Data::Dumper;
use File::Basename;
use Getopt::Long qw /:config pass_through /;

use DMWMMON::SpaceMon::NamespaceConfig;
use DMWMMON::SpaceMon::FormatFactory;
use DMWMMON::SpaceMon::StorageDump;
use DMWMMON::SpaceMon::Aggregate;
use DMWMMON::SpaceMon::RecordIO;
use DMWMMON::SpaceMon::Core;

my $me = basename($0);
my $argname='STORAGE_DUMP';
my ($upload, $save, $debug, $verbose, $level, $format, $url, $node, $configfile);
#$debug = $verbose = 0; # use these to override class defaults. 

my ($input, $dumpfile, $parser, $record, $rio, $tmp_rec_file, $rec_from_file, $config);

sub vPrint{
    my $text = shift;
    $verbose && print "$me: " . $text . "\n"; 
}

# Check useful for packaging. Print dependencies and exit:
GetOptions("deps|d" => sub {print Dumper(%INC); exit;}); 
&DMWMMON::SpaceMon::UserAgent::auth_getoptions();
GetOptions ( 'help|h' => sub { &usage },
	     'version'=> sub { print "$me version: " . 
				   DMWMMON::SpaceMon::Core::version()."\n";
			       exit;},
	     'upload-record' => \$upload,
	     'check-auth' => sub { &check_auth },
	     'save-record'   => \$save,
	     'format:s'   => \$format,
	     'debug!' => \$debug,  # enables --nodebug option
	     'verbose!' => \$verbose,	     
	     'level:i' => \$level,
	     'url:s' => \$url,
	     'node:s' => \$node,
	     'config:s' => \$configfile,
	     );

sub usage
{ 
    my $ver = DMWMMON::SpaceMon::Core::version();
    print <<EOF;
  $me - client side utility for CMS Space Monitoring. 
  Version:  $ver    
	
      Usage:    
	$me [OPTIONS] $argname
	$me --help|-h
	$me --version
	$me --check-auth

      Upload options: 
	--url           - overrides data service url
	--upload-record - upload record to central data store
	--node          - node (usually a site name) to upload data for

EOF
;
    #openDumpUsage($argname);
    &DMWMMON::SpaceMon::UserAgent::auth_usage();
    exit 0;
}

sub check_auth 
{
    # Check  Authentication: 
    print "$me: Checking authentication\n" if $verbose;
    my $smua = DMWMMON::SpaceMon::UserAgent->new (
	URL      => $url,
	FORMAT   => 'perl',
        DEBUG    => $debug,
        VERBOSE  => $verbose,
                                     );
    $smua->readAuthFromDatasvc();
    exit;
}

$input = shift;

$config = DMWMMON::SpaceMon::NamespaceConfig->new
    ( 
      DEBUG => $debug,
      VERBOSE => $verbose,
      USERCONF => $configfile,
    );

$dumpfile = DMWMMON::SpaceMon::FormatFactory->instantiate
(
 DUMPFORMAT => $format,
 DEBUG => $debug,
 VERBOSE => $verbose,
 DUMPFILE => $input,
);

print $dumpfile->dump() if $debug;
#$dumpfile->formattingHelp();

$parser = DMWMMON::SpaceMon::Aggregate->new
    (
     DEBUG   => $debug,
     VERBOSE => $verbose,
     LEVEL   => $level,
     CONFIG  => $config,
    );

print "Using parsing algorithm: \n", $parser->dump() if $debug;
$record = $parser->createRecord($config, $dumpfile);
$node && $record->setNodeName($node);
print "Dump record returned by a parser: ", $record->dump(), "\n" if $debug;

$rio = DMWMMON::SpaceMon::RecordIO->new(
    DEBUG => $debug,
    VERBOSE => $verbose,
    DATASVC => $url,
    );

$tmp_rec_file='/tmp/record.save-in-file-test';

$rio->writeToFile($record, $tmp_rec_file);

$rec_from_file = $rio->readFromFile($tmp_rec_file);

$verbose && print $rec_from_file->dump();
if ( ! $rec_from_file->matches($record)) {    
    $upload = 0;
    print "WARNING: the Record failed validation. Will not upload!\n";
    print "DETAILS: the Record read from the file does not match the original Record.\n";
}

if ($upload) {
    $rio->upload($record);
}

$rio->show($rec_from_file);
