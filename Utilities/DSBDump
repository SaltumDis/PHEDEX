#!/usr/bin/env perl

##H List dataset information from DBS database.
##H
##H Usage:
##H   DSBDump { -from RefDB | -from PhEDEx -db FILE[:SECTION] }
##H           [-details] [-files] [PATTERN...]
##H
##H -from RefDB   read information from RefDB
##H -from PhEDEx  read information from PhEDEx pseudo-bookkeeping-system
##H -db           database connection configuration parameter file
##H -details      include detailed information about files (more costly)
##H -blocks       print out block information (more costly)
##H -files        print out file GUID and LFNs (more costly)
##H PATTERN       block match constraint (owner/dataset), such as "*/bt03_*"

BEGIN { use strict; use warnings;
  my $me = $0; $me =~ s|.*/||;
  my $home = $0; $home =~ s|/[^/]+$||; $home ||= ".";
  unshift(@INC, "$home/../Toolkit/Common"); }
use UtilsHelp;
use UtilsDBS;
use TextGlob 'glob_to_regex';

my %args = (DETAILS => 0, FILES => 0);
while (scalar @ARGV)
{
    if ($ARGV[0] eq '-h') {
	&usage();
    } elsif ($ARGV[0] eq '-from' && scalar @ARGV > 1) {
	shift (@ARGV); $args{FROM} = shift (@ARGV);
    } elsif ($ARGV[0] eq '-db' && scalar @ARGV > 1) {
	shift (@ARGV); $args{DBCONFIG} = shift (@ARGV);
    } elsif ($ARGV[0] eq '-details') {
	shift (@ARGV); $args{DETAILS} = 1;
    } elsif ($ARGV[0] eq '-blocks') {
	shift (@ARGV); $args{BLOCKS} = 1;
    } elsif ($ARGV[0] eq '-files') {
	shift (@ARGV); $args{FILES} = $args{DETAILS} = 1;
    } elsif ($ARGV[0] eq '--') {
	shift (@ARGV); last;
    } elsif ($ARGV[0] =~ '^-') {
	&usage ("$0: unrecognised option $ARGV[0]\n\n");
    } else {
	last;
    }
}

$TextGlob::strict_wildcard_slash = 0;
$args{PATS} = [ map { &glob_to_regex($_) } @ARGV ];
if (!$args{FROM}
    || !($args{FROM} eq 'RefDB' || $args{FROM} eq 'PhEDEx')
    || !($args{FROM} ne 'PhEDEx' || $args{DBCONFIG}))
{
    die "Insufficient parameters, use -h for help.\n";
}

# Create appropriate dataset bookkeeping proxy
my $dbs = ($args{FROM} eq 'RefDB'
	   ? new UtilsDBS::RefDB (%args)
	   : new UtilsDBS::PhEDEx (%args));

# Get all existing datasets
my $datasets = $dbs->fetchPublishedData();

# Get all current datasets
foreach my $item (@$datasets)
{
    my $token = "$item->{OWNER}/$item->{DATASET}";
    next if (@ARGV && !grep($token =~ $_, @{$args{PATS}}));
    print "processing $token\n";
    if ($args{DETAILS} || $args{FILES} || $args{BLOCKS})
    {
        # Get the full data
        eval { $dbs->fillDatasetInfo ($item); };
        do { warn ($@); next } if $@;
    }


    # Evaluate dataset information
    if ($args{DETAILS})
    {
        my $blocks = $item->{BLOCKS};
        my $runs = $item->{RUNS};
        my $files = $item->{FILES};
        my $events = 0; map { $events += $_->{EVTS} } values %$runs;
        print " Id         = @{[$item->{ID} || '']}\n";
        print " Owner      = $item->{OWNER}\n";
        print " Dataset    = $item->{DATASET}\n";
        print " Collection = $item->{COLLECTION}\n";
        print " Datatype   = $item->{DSINFO}{InputProdStepType}\n";
        print " InputOwner = $item->{DSINFO}{InputOwnerName}\n";
        print " PUDataset  = $item->{DSINFO}{PUDatasetName}\n";
        print " PUOwner    = $item->{DSINFO}{PUOwnerName}\n";
        print " Status     = $item->{DSINFO}{CollectionStatus}\n";
        print " Blocks     = @{[scalar keys %$blocks]}\n";
        print " Runs       = @{[scalar keys %$runs]}\n";
        print " Files      = @{[scalar @$files]}\n";
        print " Events     = $events\n";
        print " Sites      = @{[sort keys %{$item->{SITES}}]}\n";
    }

    if ($args{BLOCKS})
    {
        foreach my $b (values %{$item->{BLOCKS}})
        {
	    print " Block $b->{NAME} $b->{ASSIGNMENT}\n";
        }
    }

    if ($args{FILES})
    {
        foreach my $f (@{$item->{FILES}})
        {
	    print " File $f->{GUID} $f->{INBLOCK} $f->{LFN}[0]\n";
        }
    }
}

exit 0;
